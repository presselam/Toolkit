#! /usr/bin/env perl

use 5.020;
use warnings;
use autodie;

use Arbeiter;
use File::Basename;
use Getopt::Long;
use JSON;

use Term::ANSIColor qw( colored :constants ) ;
use Term::ReadKey;
use Toolkit;

my @attrNames
    = qw( ApproximateNumberOfMessages ApproximateNumberOfMessagesDelayed
    ApproximateNumberOfMessagesNotVisible CreatedTimestamp DelaySeconds LastModifiedTimestamp
    MaximumMessageSize MessageRetentionPeriod ReceiveMessageWaitTimeSeconds VisibilityTimeout );
my %opts = ( poll => 10 );
if (
  !GetOptions(
    \%opts,   'prefix=s', 'queue=s', 'listen',
    'poll=i', 'purge',    'verbose', 'commit'
  )
    )
{
  die("Invalid incantation\n");
}

main();
exit(0);

sub main {

  my $sqs = CreateAwsClient('SQS');

  my %dispatch = (
    'summary'    => \&list_queues,
    'show-queue' => \&show_queue,
    'purge'      => \&purge_queue,
    'listen'     => \&poll_queue,
  );

  my $action = 'summary';
  $action = 'show-queue' if ( exists( $opts{'queue'} ) );
  $action = 'purge'      if ( exists( $opts{'purge'} ) );
  $action = 'listen'     if ( exists( $opts{'listen'} ) );

  $dispatch{$action}->($sqs);
}

sub poll_queue {
  my ($sqs) = @_;

  if ( !exists( $opts{'queue'} ) ) {
    message_err('must specify a queue to purge');
    return;
  }

  my %urls = _get_queue_urls($sqs);

  my $delay = 0;
  $| = 1;
  while (1) {
    my ( $x, $y, $xp, $yp ) = GetTerminalSize();
    my $status = 2 * $delay ;
    my $pad    = $x - $status - 1;

    print(  "\r\033[${pad}C" . colored( ['green on_black'], '-' x $status ) . "\r" );
    foreach my $name ( sort keys %urls ) {
      my $resp;
      eval {
        $resp = $sqs->ReceiveMessage(
          QueueUrl          => $urls{$name},
          VisibilityTimeout => 1
        );
      };
      if ($@) {
        message_err($@);
        return;
      }

      my $mesg = $resp->Messages();
      if ( scalar( @{$mesg} ) ) {
        foreach my $m ( @{$mesg} ) {
          my $body =  $m->{'Body'};
          eval{ $body = JSON->new()->utf8->pretty->encode(decode_json($body)); };
          dump_note( title => $m->{'MessageId'}, data => $body );
        }
          $delay = 0;
      } else {
        if( $delay == 12 ){
        print(  "\r\033[${pad}C" . colored( ['red on_black'], '*' x $status ) . "\r" );
        }else{
        print(  "\r\033[${pad}C" . colored( ['yellow on_black'], '*' x $status ) . "\r" );
        }
        $delay++ if ( $delay < 12 );
      }
      sleep( 5 * $delay );
    }
  }
}

sub purge_queue {
  my ($sqs) = @_;

  if ( !exists( $opts{'queue'} ) ) {
    message_err('must specify a queue to purge');
    return;
  }

  my %urls = _get_queue_urls($sqs);
  foreach my $name ( sort keys %urls ) {
    message("Purging: $name");
    eval { $sqs->PurgeQueue( QueueUrl => $urls{$name} ) };
    if ($@) {
      message_err($@);
      return;
    }
  }

  show_queue($sqs);
}

sub show_queue {
  my ($sqs) = @_;

  my $resp = $sqs->ListQueues( QueueNamePrefix => $opts{'queue'} );
  if ( scalar @{ $resp->QueueUrls() } == 0 ) {
    message_err("Unable to find queue: [$opts{'queue'}]");
    return;
  }

  my $url = shift( @{ $resp->QueueUrls() } );
  $resp = $sqs->GetQueueAttributes(
    QueueUrl       => $url,
    AttributeNames => \@attrNames
  );

  my $attr = $resp->Attributes();
  my $wide = 0;
  foreach my $a ( sort keys %{$attr} ) {
    $wide = length($a) if ( length($a) > $wide );
  }

  my %tree;
  my $name = basename($url);
  my $path = dirname($url);

  foreach my $a ( sort keys %{$attr} ) {
    my $key = sprintf( "\%-${wide}s => \%s", $a, $attr->{$a} );
    $tree{$path}{$name}{$key} = {};
  }

  dump_tree( \%tree );
}

sub list_queues {
  my ($sqs) = @_;

  my %urls = _get_queue_urls($sqs);
  my %summary;
  foreach my $url ( keys %urls ) {
    my $name = basename($url);
    my $path = dirname($url);
    $summary{$path}{$name} = {};
  }
  dump_tree( \%summary );
}

sub _get_queue_urls {
  my ($sqs) = @_;

  my $qnp = exists( $opts{'prefix'} ) ? $opts{'prefix'} : '';
  $qnp = exists( $opts{'queue'} ) ? $opts{'queue'} : $qnp;

  my %retval;
  my $queues = $sqs->ListAllQueues(
    sub {
      my $q    = shift;
      my $name = basename($q);
      $retval{$name} = $q;
    },
    QueueNamePrefix => $qnp
  );

  return wantarray ? %retval : \%retval;
}

__END__ 

=head1 NAME

/home/apressel/bin/aws.sqs.pl - [description here]

=head1 VERSION

This documentation refers to /home/apressel/bin/aws.sqs.pl version 0.0.1

=head1 USAGE

    /home/apressel/bin/aws.sqs.pl [options]

=head1 REQUIRED ARGUMENTS

=over

None

=back

=head1 OPTIONS

=over

None

=back

=head1 DIAGNOSTICS

None.

=head1 CONFIGURATION AND ENVIRONMENT

Requires no configuration files or environment variables.


=head1 DEPENDENCIES

None.


=head1 BUGS

None reported.
Bug reports and other feedback are most welcome.


=head1 AUTHOR

Andrew Pressel C<< apressel@nextgenfed.com >>


=head1 COPYRIGHT

Copyright (c) 2020, Andrew Pressel C<< <apressel@nextgenfed.com> >>. All rights reserved.

This module is free software. It may be used, redistributed
and/or modified under the terms of the Perl Artistic License
(see http://www.perl.com/perl/misc/Artistic.html)


=head1 DISCLAIMER OF WARRANTY

BECAUSE THIS SOFTWARE IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY
FOR THE SOFTWARE, TO THE EXTENT PERMITTED BY APPLICABLE LAW. EXCEPT WHEN
OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES
PROVIDE THE SOFTWARE "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
EXPRESSED OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE
ENTIRE RISK AS TO THE QUALITY AND PERFORMANCE OF THE SOFTWARE IS WITH
YOU. SHOULD THE SOFTWARE PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL
NECESSARY SERVICING, REPAIR, OR CORRECTION.

IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING
WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR
REDISTRIBUTE THE SOFTWARE AS PERMITTED BY THE ABOVE LICENCE, BE
LIABLE TO YOU FOR DAMAGES, INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL,
OR CONSEQUENTIAL DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE
THE SOFTWARE (INCLUDING BUT NOT LIMITED TO LOSS OF DATA OR DATA BEING
RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR THIRD PARTIES OR A
FAILURE OF THE SOFTWARE TO OPERATE WITH ANY OTHER SOFTWARE), EVEN IF
SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE POSSIBILITY OF
SUCH DAMAGES.

